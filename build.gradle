plugins {
    id "java"
    id "application"
}

base {
    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    compileJava.options.encoding = compileTestJava.options.encoding = javadoc.options.encoding = "UTF-8"

    group = project.maven_group ?: rootProject.maven_group
    archivesName = project.maven_name ?: rootProject.maven_name
    version = project.maven_version ?: rootProject.maven_version
}

configurations {
    include

    implementation.extendsFrom include
    api.extendsFrom include
}

repositories {
    mavenCentral()
    maven {
        name = "Lenni0451"
        url = "https://maven.lenni0451.net/snapshots"
    }
}

dependencies {
    include "net.raphimc:NoteBlockLib:3.0.1-SNAPSHOT"
    include "net.raphimc:audio-mixer:2.1.0-SNAPSHOT"
    include "org.jcraft:jorbis:0.0.17"
    include "com.formdev:flatlaf:3.5.4"
    include "net.lenni0451.commons:swing:1.7.0"
    include "org.lwjgl:lwjgl:3.3.6"
    include "org.lwjgl:lwjgl-openal:3.3.6"
    include "org.lwjgl:lwjgl-stb:3.3.6"
    ["natives-windows", "natives-windows-arm64", "natives-linux", "natives-linux-arm64", "natives-macos", "natives-macos-arm64"].each {
        include "org.lwjgl:lwjgl:3.3.6:$it"
        include "org.lwjgl:lwjgl-openal:3.3.6:$it"
        include "org.lwjgl:lwjgl-stb:3.3.6:$it"
    }
    include "net.java.dev.jna:jna:5.17.0"

    include("net.raphimc:thingl:0.0.1-20250207.205058-20") {
        exclude group: "org.slf4j", module: "slf4j-api"
    }
    include "org.lwjgl:lwjgl-glfw:3.3.6"
    include "org.lwjgl:lwjgl-opengl:3.3.6"
    include "org.lwjgl:lwjgl-freetype:3.3.6"
    ["natives-windows", "natives-windows-arm64", "natives-linux", "natives-linux-arm64"].each {
        include "org.lwjgl:lwjgl-glfw:3.3.6:$it"
        include "org.lwjgl:lwjgl-opengl:3.3.6:$it"
        include "org.lwjgl:lwjgl-freetype:3.3.6:$it"
    }
}

application {
    mainClass = "net.raphimc.noteblocktool.Main"
}

java {
    withSourcesJar()
}

jar {
    dependsOn configurations.include
    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.include.collect {
            zipTree(it)
        }
    } {
        exclude "META-INF/*.RSA", "META-INF/*.SF", "META-INF/*.DSA"
    }

    manifest {
        attributes(
                "Main-Class": application.mainClass,
                "Multi-Release": "true"
        )
    }
}
